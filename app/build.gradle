apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.light.weather"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "HEWEATHER_KEY", HEWEATHER_KEY
        buildConfigField "String", "BASE_URL", BASE_URL
        buildConfigField "String", "APP_ID", APP_ID
        manifestPlaceholders = [amap_key: AMAP_KEY]
    }

    def filePrivateProperties = file("../jks/public.properties")
    if (filePrivateProperties.exists()) {
        signingConfigs {
            release {
                Properties propsPrivate = new Properties()
                propsPrivate.load(new FileInputStream(filePrivateProperties))

                storeFile file(propsPrivate['key.store'])
                keyAlias propsPrivate['key.alias']
                storePassword propsPrivate['key.store.password']
                keyPassword propsPrivate['key.alias.password']
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->//获取 variant 参数，就是 productFlavor x buildType
        variant.outputs.all {
            if (outputFileName.endsWith('-release.apk')) {//变种名以release.apk结尾时才修改名字
                outputFileName = outputFileName.replace(outputFileName, "LightWeather" + "_" + variant.versionName + ".apk")
            } else if (outputFileName.endsWith('-debug.apk')) {
                outputFileName = outputFileName.replace(outputFileName, "LightWeather-debug" + "_" + variant.versionName + ".apk")
            }
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            if (filePrivateProperties.exists()) {
                signingConfig signingConfigs.release
            }
        }
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            if (filePrivateProperties.exists()) {
                signingConfig signingConfigs.release
            }
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation "junit:junit:$rootProject.junitVersion"
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'

    //android support
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    //constraint-layout
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    //swipe layout
    implementation "com.github.anzaizai:EasySwipeMenuLayout:$rootProject.EasySwipeMenuLayout"

    //RecyclerView Adapter Helper
    implementation "com.github.CymChad:BaseRecyclerViewAdapterHelper:$rootProject.adapterHelperVersion"

    //butterknife
    implementation "com.jakewharton:butterknife:$rootProject.butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknifeVersion"

    //rxjava
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxjavaVersion"
    //rxandroid
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroidVersion"
    //rxpermissions
    implementation "com.github.tbruyelle:rxpermissions:$rootProject.rxpermissionsVersion"
    //rxbinding
    implementation "com.jakewharton.rxbinding2:rxbinding:$rootProject.rxbinding"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rootProject.rxbinding"

    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    //retrofit-url-manager
    implementation "me.jessyan:retrofit-url-manager:$rootProject.urlManager"
    //facebook stetho for debug http
    implementation "com.facebook.stetho:stetho-okhttp3:$rootProject.stetho"

    // Dagger dependencies
    implementation "com.google.dagger:dagger:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    // if you use the support libraries
    annotationProcessor "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"

    //leakcanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.leakcanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakcanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakcanaryVersion"

    //sqlite on web
    debugImplementation "io.github.skyhacker2:sqliteonweb:$rootProject.sqliteonweb"
    releaseImplementation "io.github.skyhacker2:sqliteonweb-no-op:$rootProject.sqliteonweb"

    //高德定位
    implementation 'com.amap.api:location:latest.integration'

    //Tencent bugly 其中latest.release指代最新Bugly SDK版本号，也可以指定明确的版本号，例如2.2.0
    implementation 'com.tencent.bugly:crashreport:latest.release'

    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    //flowlayout
    implementation 'com.hyman:flowlayout-lib:1.1.2'
}
